## FALLBACK FOR "PREFIX" OPTION WHICH IS DEPRECATED NOW & SHOULD BE DELETED IN THE FUTURE
if( PREFIX )
    set(INSTALL_PATH "${PREFIX}")
endif()

## SHORTHAND FOR CMAKE_INSTALL_PREFIX
if( INSTALL_PATH )
    file(TO_CMAKE_PATH "${INSTALL_PATH}" CMAKE_INSTALL_PREFIX)
endif()


### AUTHSERVER & WORLDSERVER

## BIN PATH
if ( SERVERS_BIN_PATH )
    set(AUTHSERVER_BIN_PATH "${SERVERS_BIN_PATH}")
    set(WORLDSERVER_BIN_PATH "${SERVERS_BIN_PATH}")
elseif ( SERVERS_BIN_DIR )
    set(AUTHSERVER_BIN_PATH "${CMAKE_INSTALL_PREFIX}/${SERVERS_BIN_DIR}")
    set(WORLDSERVER_BIN_PATH "${CMAKE_INSTALL_PREFIX}/${SERVERS_BIN_DIR}")
else()
    set(AUTHSERVER_BIN_PATH "${CMAKE_INSTALL_PREFIX}/${AUTHSERVER_BIN_DIR}")
    set(WORLDSERVER_BIN_PATH "${CMAKE_INSTALL_PREFIX}/${WORLDSERVER_BIN_DIR}")

    if ( NOT AUTHSERVER_BIN_DIR)
        set(AUTHSERVER_BIN_MSG "(default location)")
    endif()
    if ( NOT WORLDSERVER_BIN_DIR)
        set(WORLDSERVER_BIN_MSG "(default location)")
    endif()
endif()

## CONFIGURATION PATH
if ( SERVERS_CONF_PATH )
    set(AUTHSERVER_CONF_PATH "${SERVERS_CONF_PATH}")
    set(WORLDSERVER_CONF_PATH "${SERVERS_CONF_PATH}")
else() ## Default = same location as the binaries
    if ( NOT AUTHSERVER_CONF_PATH)
        set(AUTHSERVER_CONF_PATH "${AUTHSERVER_BIN_PATH}")
        set(AUTHSERVER_CONF_MSG "(default location, alongside the binaries)")
    endif()
    if ( NOT WORLDSERVER_CONF_PATH)
        set(WORLDSERVER_CONF_PATH "${WORLDSERVER_BIN_PATH}")
        set(WORLDSERVER_CONF_MSG "(default location, alongside the binaries)")
    endif()
endif()


### MODULES CONFIGURATION

## DEFAULT PATH FOR MODULES CONF CAN BE OVERRIDEN
if( NOT MODULES_CONF_PATH )

    if( NOT MODULES_CONF_DIR )
      set(MODULES_CONF_DIR "modules_conf")
      set(MODULES_CONF_MSG "(default location)")
    endif()

    set(MODULES_CONF_PATH "${WORLDSERVER_BIN_PATH}/${MODULES_CONF_DIR}")
endif()

## COMPATIBILITY WITH BADLY CODED (AND OLD) MODULES
set(CONF_DIR "${MODULES_CONF_PATH}")


### CONVERT TO CMAKE PATHES

file(TO_CMAKE_PATH "${AUTHSERVER_BIN_PATH}" AUTHSERVER_BIN_PATH)
file(TO_CMAKE_PATH "${WORLDSERVER_BIN_PATH}" WORLDSERVER_BIN_PATH)
file(TO_CMAKE_PATH "${AUTHSERVER_CONF_PATH}" AUTHSERVER_CONF_PATH)
file(TO_CMAKE_PATH "${WORLDSERVER_CONF_PATH}" WORLDSERVER_CONF_PATH)
file(TO_CMAKE_PATH "${MODULES_CONF_PATH}" MODULES_CONF_PATH)


### STATUS MESSAGES

message(STATUS "INSTALL: Installing files in ${CMAKE_INSTALL_PREFIX}/")
message(STATUS "INSTALL: Installing AUTHSERVER binary in ${AUTHSERVER_BIN_PATH}/ ${WORLDSERVER_BIN_MSG}")
message(STATUS "INSTALL: Installing AUTHSERVER configuration file in ${AUTHSERVER_CONF_PATH}/ ${AUTHSERVER_CONF_MSG}")
message(STATUS "INSTALL: Installing WORLDSERVER binary in ${WORLDSERVER_BIN_PATH}/ ${WORLDSERVER_BIN_MSG}")
message(STATUS "INSTALL: Installing WORLDSERVER configuration file in ${WORLDSERVER_CONF_PATH}/ ${WORLDSERVER_CONF_MSG}")
message(STATUS "INSTALL: Installing MODULES configuration files in ${MODULES_CONF_PATH}/ ${MODULES_CONF_MSG}")
